apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'application'
sourceCompatibility = 1.8
version = '1.0'
mainClassName = "com.bobgettys.jsh.Main"

project.ext.appName = "Jsh"
project.ext.jarManifest = {
	attributes 'Implementation-Title': appName, 
		'Implementation-Version': version,
		'Built-Date': new Date(),
        'Built-JDK': System.getProperty('java.version'),
        'Main-Class': mainClassName
} 


task slimJar(type: Jar) {
	from files(sourceSets.main.output.classesDir)
	manifest jarManifest
}

jar {
	baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest jarManifest
}
jar.dependsOn slimJar

repositories {
	mavenCentral()
}

dependencies {
	compile 'commons-io:commons-io:2.4'
	compile 'commons-cli:commons-cli:1.2'
	compile 'org.apache.commons:commons-lang3:3.3.2'
}

uploadArchives {
	repositories {
		flatDir {
			dirs '~/bin/'
		}
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

task pom << {
	pom {
		project {
			groupId 'com.bobgettys.'
			artifactId 'jsh'
			version '1.0'
			inceptionYear '2014'
			licenses {
				license {
					name 'GNU GENERAL PUBLIC LICENSE Version 2'
					url 'https://www.gnu.org/licenses/gpl-2.0.txt'
				}
			}
		}
	}.writeTo("$projectDir/pom.xml")
}

build.dependsOn pom
